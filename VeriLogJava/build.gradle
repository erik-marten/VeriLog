import java.time.Year

plugins {
    id "java-library"
    id "com.gradleup.shadow" version "9.3.1"
    id "application"
}

group = "io.github.em"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 11
}

dependencies {
    api "org.bouncycastle:bcprov-jdk18on:1.78.1"
    api "org.bouncycastle:bcpkix-jdk18on:1.78.1"
    api "com.fasterxml.jackson.core:jackson-databind:2.17.2"

    testImplementation platform("org.junit:junit-bom:5.10.3")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}
application {
    mainClass = "io.github.em.verilog.cli.VeriLogCli"}



def currentYear = Year.now().value
def author = "Erik Marten"

tasks.register("generateLicense", Copy) {
    from("LICENSE.template")
    into("$buildDir/generated")
    rename { "LICENSE" }

    expand(
            year: currentYear,
            author: author
    )
    filteringCharset = "UTF-8"
}

/**
 * build FatJar:
 *    build/libs/<name>-<version>-all.jar
 */
shadowJar {
    archiveClassifier.set("all")
    mergeServiceFiles()
    dependsOn tasks.generateLicense

    exclude("META-INF/LICENSE*", "META-INF/NOTICE*", "META-INF/DEPENDENCIES")

    from("$buildDir/generated/LICENSE") { into("META-INF") }

    from("NOTICE") { into("META-INF"); rename { "NOTICE-VERILOGGER" } }

    from("THIRD-PARTY-LICENSES") { into("META-INF") }

    manifest {
        attributes "Main-Class": application.mainClass.get()
    }
}
tasks.build {
    dependsOn tasks.shadowJar
}
test {
    useJUnitPlatform()
}
